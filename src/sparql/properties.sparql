# This is an example query for properties along with their domain classes, range classes, and inverse properties
# It also returns whether the property is functional and whether its inverse (if any) is functional

PREFIX owl:   <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT DISTINCT ?domain ?property ?range ?functional ?inverse ?inverseFunctional

WHERE {
	?domain a owl:Class .
	?property rdfs:domain ?domain .
	?property rdfs:range ?range .
	BIND(EXISTS{?property a owl:FunctionalProperty} AS ?functional)
	OPTIONAL { 
		?property owl:inverseOf ?inverse . 
		BIND(EXISTS{?property a owl:InverseFunctionalProperty} AS ?inverseFunctional)
	}
}

ORDER BY ?domain
